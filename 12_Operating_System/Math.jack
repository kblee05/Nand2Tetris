// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack
/**
 *  A library of commonly used mathematical functions.
 *  All functions runs in O(n), where n is the number of bits used
 *  for representing a two's complement integer value (16 in the Hack computer). 
 *  Note: Jack compilers implement multiplication and division
 *  using calls to OS functions in this class.
 */
class Math {
    static int n;             // Number of bits used for representing a two's complement integer
    static Array powersOfTwo; // Stores 2^0, 2^1, 2^2,..., 2^(n-1)

    // Initializes the Math library.
    function void init() {
        var int i;
        let i = 1;
        let powersOfTwo = Array.new(16);
        let powersOfTwo[0] = 1;

        while(i < 16){
            let powersOfTwo[i] = powersOfTwo[i-1] * 2;
            let i = i + 1;
        }

        return;
    }

    function boolean bit(int x, int i){
        return ~((x & powersOfTwo[i]) = 0);
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*'
     *  in an expression, it handles it by invoking this method. 
     *  Thus, in Jack, x * y and Math.multiply(x,y) return the same value. */
    function int multiply(int x, int y) {
        var int sum, x_shifted, i;
        let sum = 0;
        let x_shifted = x;
        let i = 0;

        while(i < 16){
            if(Math.bit(y, i)){
                let sum = sum + x_shifted;
            }
            let x_shifted = x_shifted + x_shifted;
            
            let i = i + 1;
        }

        return sum;
    }

    /** Returns the integer part of x / y.
     *  When a Jack compiler detects the division operator '/'
     *  an an expression, it handles it by invoking this method.
     *  Thus, x/y and Math.divide(x,y) return the same value. */
    function int divide(int x, int y) {
        var int q;
        var int absX, absY;
        var int result;
        
        let absX = Math.abs(x);
        let absY = Math.abs(y);

        if (absY > absX) {
            return 0;
        }
        
        let q = absX / (absY + absY); 

        if ((absX - (2 * q * absY)) < absY) {
            let result = q + q;
        } else {
            let result = q + q + 1;
        }

        if ((x < 0) = (y < 0)) { 
            return result; 
        } else {
            return -result;
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, j, temp;
        let y = 0;
        let j = 7;

        while(j > 0 | j = 0){
            let temp = (y + powersOfTwo[j]) * (y + powersOfTwo[j]);

            if( ~(temp > x) & (temp > 0) ){
                let y = y + powersOfTwo[j];
            }

            let j = j - 1;
        }

        return y;
    }

    /** Returns the greater value. */
    function int max(int a, int b) {
        if(a > b){
            return a;
        }
        return b;
    }

    /** Returns the smaller value. */
    function int min(int a, int b) {
        if(a < b){
            return a;
        }
        return b;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if(x < 0){
            return -x;
        }
        return x;
    }
}
