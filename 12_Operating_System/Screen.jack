// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack
/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 512 rows (indexed 0..511, top to bottom)
 * of 256 pixels each (indexed 0..255, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static boolean color;
    static Array powersOfTwo;

    /** Initializes the Screen. */
    function void init() {
        var int i;
        let color = true;
        let i = 1;
	    let powersOfTwo = Array.new(16);
        let powersOfTwo[0] = 1;
        while(i<16){
            let powersOfTwo[i] = powersOfTwo[i-1] + powersOfTwo[i-1];
            let i = i + 1;
        }
        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int i;
        let i = 0;
        while(i<8192){
            do Memory.poke(16384 + i, false);
            let i = i + 1;
        }
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let color = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int address, value, mask;
        let address = 16384 + (32 * y) + (x / 16);
        let mask = powersOfTwo[x - (16 * (x / 16))];
        let value = Memory.peek(address);
        if(color =true){ // black
            let value = value | mask;
        }
        else{ // white
            let value = value & ~mask;
        }
        do Memory.poke(address, value);
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int dx, dy;
        var int a, b;
        var int diff;
	    var int x_step, y_step;
        var int temp;

        if (y1 = y2) { // horizontal line
            if (x1 > x2) {
                let temp = x1;
                let x1 = x2;
                let x2 = temp;
            }
            while (~(x1 > x2)) {
                do Screen.drawPixel(x1, y1);
                let x1 = x1 + 1;
            }
            return;
        }

        let dx = Math.abs(x2 - x1);
        let dy = Math.abs(y2 - y1);
	
	    if(x1 < x2){
            let x_step = 1;
	    }
        else{
            let x_step = -1;
        }
        if(y1 < y2){
            let y_step = 1;
        }
        else{
            let y_step = -1;
        }
	
        let a = 0;
        let b = 0;
        let diff = 0;
        while(~(a > dx) & ~(b > dy)){
            do Screen.drawPixel(x1 + (a * x_step), y1 + (b * y_step));
            if(diff < 0){
                let a = a + 1;
                let diff = diff + dy;
            }
            else{
                let b = b + 1;
                let diff = diff - dx;
            }
        }

        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     *  and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int x;
        let x = x1;
        while(~(x>x2)){
            do Screen.drawLine(x, y1, x, y2);
            let x = x + 1;
        }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dx, dist;
        let dx = -r;
        while(~(dx>r)){
            let dist = Math.sqrt((r * r) - (dx * dx));
            do Screen.drawLine(x + dx, y - dist, x + dx, y + dist);
	    let dx = dx + 1;
        }
        return;
    }
}
