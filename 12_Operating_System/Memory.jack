// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack
/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {
    static Array ram;
    static Array heap;
    static int free_list;

    /** Initializes the class. */
    function void init() {
        let ram = 0;
        let heap = 2048;
        let free_list = heap;
        let heap[0] = 0;
        let heap[1] = 14334;

        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return ram[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let ram[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function Array alloc(int size) {
        var Array segment, address;
        let segment = free_list;
        while(segment[1] < (size + 2)){ // first fit
            let segment = segment[0];
        }
        let address = segment[1] + segment - size;
        let address[1] = size;
        let segment[1] = segment[1]- (size + 2);
        return address + 2;
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        var Array segment;
        let segment = o - 2;
        let segment[0] = free_list;
        let free_list = segment;
        return;
    }    
}
